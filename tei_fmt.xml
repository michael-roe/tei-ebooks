<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
xmlns:tei="http://www.tei-c.org/ns/1.0"
xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<xsl:template match="/">
<html>
<body>

<h1 style="text-align:center"><xsl:value-of select="tei:TEI/tei:teiHeader/tei:fileDesc/tei:titleStmt/tei:title"/></h1>

<p style="text-align:center">By <xsl:value-of select="tei:TEI/tei:teiHeader/tei:fileDesc/tei:titleStmt/tei:author"/></p>

<xsl:apply-templates select="/tei:TEI/tei:text/tei:body"/>

</body>
</html>
</xsl:template>

<!-- TEI Chapter 4: Default Text Structure -->

<xsl:template match="tei:div">
<section>
<xsl:choose>
<xsl:when test="@subtype='book'">
<h2>Book <xsl:value-of select="@n"/></h2>
</xsl:when>
<xsl:when test="@subtype='number'">
<h3><xsl:value-of select="@n"/></h3>
</xsl:when>
</xsl:choose>
<xsl:apply-templates select="child::*"/>
</section>
</xsl:template>

<xsl:template match="tei:div1">
<div>
<xsl:apply-templates select="child::*"/>
<p style="margin-left: 6em; text-align:left">==========</p>
</div>
</xsl:template>

<xsl:template match="tei:div2">
<div>
<xsl:apply-templates select="child::*"/>
<p style="margin-left: 6em; text-align:left" >----------</p>
</div>
</xsl:template>

<!-- TEI Chapter 3: Elements available in all documents -->

<!-- 3.1 Paragraphs -->

<xsl:template match="tei:p">
<p>
<xsl:apply-templates select="node()"/>
</p>
</xsl:template>

<!-- 3.3 Highlighting and Quotation -->

<xsl:template match="tei:q">
<xsl:choose>
<xsl:when test="@part='I'">
&#x201c;<xsl:value-of select="node()"/>
</xsl:when>
<xsl:when test="@part='M'">
<xsl:value-of select="node()"/>
</xsl:when>
<xsl:when test="@part='F'">
<xsl:value-of select="node()"/>&#x201d;
</xsl:when>
<xsl:otherwise>
&#x201c;<xsl:value-of select="node()"/>&#x201d;
</xsl:otherwise>
</xsl:choose>
</xsl:template>

<!-- 3.4 Terms and Glosses -->

<xsl:template match="tei:term">
<span style="font-weight: bold"><b><xsl:apply-templates select="node()"/></b></span>
</xsl:template>

<!-- 3.5 Simple Editorial Changes -->

<xsl:template match="tei:add">
&#x2039;<span style="color:blue"><xsl:value-of select="node()"/></span>&#x203a;
</xsl:template>

<xsl:template match="tei:del">
<span style="text-decoration-line: line-through"><xsl:value-of select="node()"/></span>
</xsl:template>

<!-- 3.6 Names, Numbers, Dates -->

<xsl:template match="tei:num">
<span style="font-weight: bold"><xsl:apply-templates select="node()"/></span>
</xsl:template>

<!-- 3.10 Graphics -->

<xsl:template match="tei:figure">
</xsl:template>

<!-- 3.13 Passages of Verse or Drama -->

<xsl:template match="tei:sp">
<xsl:apply-templates select="child::*"/>
</xsl:template>

<xsl:template match="tei:speaker">
<p style="margin-left:6em; font-weight:bold"><xsl:value-of select="."/></p>
</xsl:template>

<xsl:template match="tei:stage">
<p style="margin-left: 6em; color:green">
[<xsl:apply-templates select="node()"/>]
</p>
</xsl:template>

<xsl:template match="tei:l">
<xsl:choose>
<xsl:when test="@part='M'">&#xA0;&#xA0;</xsl:when>
<xsl:when test="@part='F'">&#xA0;&#xA0;&#xA0;&#xA0;</xsl:when>
<xsl:otherwise> </xsl:otherwise>
</xsl:choose>
<xsl:apply-templates select="node()"/><br/>
</xsl:template>

<!-- 3.11 Reference Systems -->

<xsl:template match="tei:milestone">
[page]
</xsl:template>

<xsl:template match="tei:lb">
[line]
</xsl:template>

<xsl:template match="tei:seg">
<xsl:choose>
<xsl:when test="@type='doubtful'">
&#x2020;<span style="color:blue"><xsl:value-of select="node()"/></span>&#x2020;
</xsl:when>
<xsl:otherwise>
<span style="color:magenta"><xsl:value-of select="node()"/></span>
</xsl:otherwise>
</xsl:choose>
</xsl:template>

<!-- Chapter 13: Critical Apparatus -->

<xsl:template match="tei:app">
<p style="color:red">
<xsl:apply-templates select="node()"/>
</p>
</xsl:template>

<xsl:template match="tei:rdg">
<xsl:apply-templates select="node()"/>
</xsl:template>

<xsl:template match="tei:wit">
&#x2014;<xsl:apply-templates select="node()"/><br/>
</xsl:template>

<!-- Gaps and metamarks -->

<xsl:template match="tei:gap">
. . . .
</xsl:template>

<xsl:template match="tei:metamark">
<xsl:choose>
<xsl:when test="@function='lacuna'">
* * * * *
</xsl:when>
<xsl:when test="@function='insert'">
+++
</xsl:when>
<xsl:otherwise>
<xsl:value-of select="."/>
</xsl:otherwise>
</xsl:choose>
</xsl:template>

<!-- Chapter 14: Names, Dates, People and Places -->

<xsl:template match="tei:listPlace">
<ul>
<xsl:apply-templates select="node()"/>
</ul>
</xsl:template>

<xsl:template match="tei:place">
<li>
<xsl:value-of select="tei:placeName"/><xsl:if test="tei:location/tei:country">, <xsl:value-of select="tei:location/tei:country"/></xsl:if>:
<xsl:if test="tei:location/tei:geo"><xsl:value-of select="tei:location/tei:geo"/></xsl:if>
</li>
</xsl:template>

<xsl:template match="tei:placeName">
<xsl:apply-templates select="node()"/>
</xsl:template>

<xsl:template match="tei:location">
<xsl:apply-templates select="node()"/>
</xsl:template>

<xsl:template match="tei:geo">
(<xsl:apply-templates select="node()"/>)
</xsl:template>

</xsl:stylesheet>
